# PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Builds the V4L2 Relay for the IPU6 devices. See https://code.launchpad.net/v4l2-relayd
_pkgname=v4l2-relayd
_host=git.launchpad.net
pkgname=${_pkgname}
pkgver=r42.6fd6b6a
pkgrel=1
pkgdesc="V4L2-relayd (Alder Lake)"
arch=('x86_64')
url="https://${_host}/${_pkgname}"
license=('unknown')
makedepends=('git')
source=("git://${_host}/${_pkgname}#tag=upstream/0.1.3" "0001-Fix-V4L2_EVENT_PRI_CLIENT_USAGE-to-use-value-used-up.patch")
sha256sums=('SKIP' 'SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$_pkgname"
    patch -Np1 -i ../0001-Fix-V4L2_EVENT_PRI_CLIENT_USAGE-to-use-value-used-up.patch
    ./autogen.sh --prefix=/usr
    make
}


package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/etc" "$pkgdir/etc"
	sed -i 's/FORMAT=YUY2/FORMAT=NV12/g' "$pkgdir/etc/default/v4l2-relayd"
	sed -i 's/VIDEOSRC="videotestsrc"/VIDEOSRC="icamerasrc"/g' "$pkgdir/etc/default/v4l2-relayd"
	sed -i 's/DeviceAllow=char-video4linux//g' "$pkgdir/usr/lib/systemd/system/v4l2-relayd.service"
  # Remove `CapabilityBoundingSet=` from the service file to prevent crashing 
  # v4l2-relayd on Wayland.
	sed -i 's/CapabilityBoundingSet=//g' "$pkgdir/usr/lib/systemd/system/v4l2-relayd.service"
}

